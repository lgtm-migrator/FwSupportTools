# Run and debug from one command:
#   Build/run-in-environ gdb mono-sgen --ex "run --debug $HOME/fwrepo/fw/Output/Debug/FieldWorks.exe"
# Run and debug from separate terminals:
#   Build/run-in-environ mono-sgen --debug $HOME/fwrepo/fw/Output/Debug/FieldWorks.exe
#   Build/run-in-environ gdb mono-sgen $(pgrep -f FieldWorks.exe)
#
# http://www.mono-project.com/docs/debug+profile/debug/

handle SIGXCPU SIG33 SIG35 SIG36 SIG37 SIG38 SIGPWR SIG38 nostop noprint

define mono_backtrace
 select-frame 0
 set $i = 0
 while ($i < $arg0)
   set $foo = (char*) mono_pmip ($pc)
   if ($foo)
     printf "#%d %p in %s\n", $i, $pc, $foo
   else
     frame
   end
   up-silently
   set $i = $i + 1
 end
end

define mono_stack
 set $mono_thread = mono_thread_current ()
 if ($mono_thread == 0x00)
   printf "No mono thread associated with this thread\n"
 else
   set $ucp = malloc (sizeof (ucontext_t))
   call (void) getcontext ($ucp)
   call (void) mono_print_thread_dump ($ucp)
   call (void) free ($ucp)
 end
end

add-auto-load-safe-path /opt/mono5-sil/bin/mono-sgen-gdb.py
